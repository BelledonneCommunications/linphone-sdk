dnl Applied patch to handle AC_ARG_ENABLE targets (Patch 1250604) - Provided by Andrew Kephart <andrew.kephart@alereon.com

AC_INIT(CUnit, 2.0-3)
AM_INIT_AUTOMAKE(CUnit, 2.0-3)

PACKAGE=CUnit
VERSION_MAJOR=2
VERSION_MINOR=0
VERSION=${VERSION_MAJOR}.${VERSION_MINOR}
RELEASE=3

TOPDIR=`pwd`

CFLAGS="$CFLAGS -DRELEASE=@RELEASE@ -Wall -W -pedantic -Wshadow -ansi -I${PWD}/CUnit/Headers"
LDFLAGS="$LDFLAGS -L${PWD}/CUnit/Sources"

AC_ARG_ENABLE(debug,
  [  --enable-debug    enable debugging],
  [cu_do_debug=$enableval],
  [cu_do_debug="no"])
if test x"$cu_do_debug" = xyes ; then
	echo "++++++++++ Enabling debug mode compilation."
	CFLAGS="$CFLAGS -D_DEBUG -g3 -Wall"
	LDFLAGS="$LDFLAGS -g"
else
	echo "---------- Disabling debug mode compilation."
fi


AC_ARG_ENABLE(basic,
  [  --enable-basic    enable basic mode compilation],
  [cu_do_basic=$enableval],
  [cu_do_basic="no"])

AM_CONDITIONAL(ENABLE_BASIC, test x"$cu_do_basic" = xyes)
if test x"$cu_do_basic" = xyes ; then
	echo "++++++++++ Enabling basic mode compilation"
else
	echo "---------- Disabling basic mode compilation"
fi


AC_ARG_ENABLE(console,
  [  --enable-console    enable console mode compilation],
  [cu_do_console=$enableval],
  [cu_do_console="no"])

AM_CONDITIONAL(ENABLE_CONSOLE, test x"$cu_do_console" = xyes)
if test x"$cu_do_console" = xyes ; then
	echo "++++++++++ Enabling console mode compilation"
else
	echo "---------- Disabling console mode compilation"
fi


AC_ARG_ENABLE(automated,
  [  --enable-automated    enable automated mode compilation],
  [cu_do_automated=$enableval],
  [cu_do_automated="no"])

AM_CONDITIONAL(ENABLE_AUTOMATED, test x"$cu_do_automated" = xyes)
if test x"$cu_do_automated" = xyes ; then
	echo "++++++++++ Enabling automated(XML) mode compilation"
else
	echo "---------- Disabling automated(XML) mode compilation"
fi

AC_ARG_ENABLE(curses,
  [  --enable-curses    enable curses mode compilation],
  [cu_do_curses=$enableval],
  [cu_do_curses="no"])

AM_CONDITIONAL(ENABLE_CURSES, test x"$cu_do_curses" = xyes)
if test x"$cu_do_curses" = xyes ; then
	echo "++++++++++ Enabling curses mode compilation"
else
	echo "---------- Disabling curses mode compilation"
fi

AC_ARG_ENABLE(examples,
  [  --enable-examples    enable examples mode compilation],
  [cu_do_examples=$enableval],
  [cu_do_examples="no"])

AM_CONDITIONAL(ENABLE_EXAMPLES, test x"$cu_do_examples" = xyes)
if test x"$cu_do_examples" = xyes ; then
	echo "++++++++++ Enabling examples mode compilation"
else
	echo "---------- Disabling examples mode compilation"
fi

AC_ARG_ENABLE(test,
  [  --enable-test    enable test mode compilation],
  [cu_do_test=$enableval],
  [cu_do_test="no"])

AM_CONDITIONAL(ENABLE_TEST, test x"$cu_do_test" = xyes)
if test x"$cu_do_test" = xyes ; then
	echo "++++++++++ Enabling test mode compilation"
else
	echo "---------- Disabling test mode compilation"
fi

AC_ARG_ENABLE(memtrace,
  [--enable-memtrace    enable memory tracing],
  [cu_do_memtrace=$enableval],
  [cu_do_memtrace="no"])
if  test x"$cu_do_memtrace" = xyes ; then
	echo "++++++++++ Enabling memtrace functionality at compile time."
	CFLAGS="$CFLAGS -DMEMTRACE"
else
	echo "---------- Disabling memtrace functionality at compile time."
fi

dnl Check for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL

dnl Check for libraries
AC_CHECK_LIB(c, main)
AC_SEARCH_LIBS([initscr],[ncurses curses],[],[echo curses library not found],[])

dnl Check for standard functions
AC_CHECK_FUNC(malloc, [], [echo malloc not found; exit 1])
AC_CHECK_FUNC(free, [], [echo free not found; exit 1])
AC_CHECK_FUNC(calloc, [], [echo calloc not found; exit 1])
AC_CHECK_FUNC(realloc, [], [echo realloc not found; exit 1])

AC_CHECK_FUNC(strcpy, [], [echo strcpy not found; exit 1])
AC_CHECK_FUNC(strerror, [], [echo strerror not found; exit 1])

AC_CHECK_FUNC(fopen, [], [echo fopen not found; exit 1])
AC_CHECK_FUNC(fclose, [], [echo fclose not found; exit 1])
AC_CHECK_FUNC(fprintf, [], [echo fprintf not found; exit 1])
AC_CHECK_FUNC(setvbuf, [], [echo setvbuf not found; exit 1])

AC_CHECK_FUNC(time, [], [echo time not found; exit 1])
AC_CHECK_FUNC(ctime, [], [echo ctime not found; exit 1])

dnl Check for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(assert.h)
AC_CHECK_HEADERS(ctype.h)
AC_CHECK_HEADERS(math.h)
AC_CHECK_HEADERS(file.h)
AC_CHECK_HEADERS(stdio.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(string.h)

AM_CONFIG_HEADER(config.h)

dnl Check for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Configure Jamrules for user environment
AC_CONFIG_FILES([Jamrules:Jamrules.in])

dnl Set environment variables.
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(RELEASE)

dnl Configure Makefile set.
AC_CONFIG_FILES( Makefile \
		CUnit.spec \
		CUnit/Makefile \
		CUnit/Headers/Makefile \
		CUnit/Headers/CUnit.h \
		CUnit/Sources/Makefile \
		CUnit/Sources/Framework/Makefile \
		CUnit/Sources/Automated/Makefile \
		CUnit/Sources/Basic/Makefile \
		CUnit/Sources/Console/Makefile \
		CUnit/Sources/Curses/Makefile \
		CUnit/Sources/Test/Makefile \
		doc/Makefile \
		doc/headers/Makefile \
		Examples/Makefile \
		Examples/AutomatedTest/Makefile \
		Examples/BasicTest/Makefile \
		Examples/ConsoleTest/Makefile \
		Examples/CursesTest/Makefile \
		Man/Makefile \
		Man/man3/Makefile \
		Share/Makefile )

AC_OUTPUT

